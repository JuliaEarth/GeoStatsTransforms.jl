# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENSE in the project root.
# ------------------------------------------------------------------

"""
    SLIC(k, m; tol=1e-4, maxiter=10, weights=nothing)

Assign labels to rows of geotable using the Simple Linear
Iterative Clustering (SLIC) algorithm.

The algorithm produces approximately `k` clusters by combining
a geospatial distance `dâ‚›` and a distance between variables `dáµ¥`.
The tradeoff is controlled with a hyperparameter `m` in an
additive model `dâ‚œ = âˆš(dáµ¥Â² + mÂ²(dâ‚›/s)Â²)` where `s` is the
average spacing between cluster centroids.

## Options

* `tol`     - Tolerance of k-means algorithm (default to `1e-4`)
* `maxiter` - Maximum number of iterations (default to `10`)
* `weights` - Dictionary with weights for each variable (default to `nothing`)

## Examples

```julia
SLIC(10, 0.01) # default options
SLIC(10, 0.01, tol=1e-2) # set tolerance
```

## References

* Achanta et al. 2011. [SLIC superpixels compared to state-of-the-art
  superpixel methods](https://ieeexplore.ieee.org/document/6205760)

### Notes

The variables (or features) are standardized with the `StdFeats`
transform prior to the core algorithm in order to facilitate the
choice of the parameter `m`.
"""
struct SLIC{W} <: TableTransform
  k::Int
  m::Float64
  tol::Float64
  maxiter::Int
  weights::W
end

function SLIC(k::Int, m::Real; tol=1e-4, maxiter=10, weights=nothing)
  @assert tol > 0 "tolerance must be positive"
  @assert maxiter > 0 "maximum number of iterations must be positive"
  SLIC{typeof(weights)}(k, m, tol, maxiter, weights)
end

function apply(transform::SLIC, geotable::AbstractGeoTable)
  # retrieve parameters
  k = transform.k
  m = transform.m
  tol = transform.tol
  maxiter = transform.maxiter
  weights = transform.weights

  # normalize attributes
  Î© = geotable |> StdFeats()
  ğ’Ÿ = domain(Î©)

  # initial spacing of clusters
  s = slic_spacing(ğ’Ÿ, k)

  # initialize cluster centers
  centers = slic_initialization(ğ’Ÿ, s)

  # initialize search method
  searcher = BallSearch(ğ’Ÿ, MetricBall(maximum(s)))

  # define table distance
  td = TableDistance(normalize=false, weights=weights)

  # pre-allocate memory for label and distance
  labels = fill(0, nelements(ğ’Ÿ))
  dists = fill(Inf, nelements(ğ’Ÿ))

  # Lloyd's (a.k.a. k-means) algorithm
  iter = 0
  Î´cur = mean(dists)
  while iter < maxiter
    slic_assignment!(Î©, searcher, td, m, s, centers, labels, dists)
    slic_update!(Î©, centers, labels)

    # average distance to centers
    Î´new = mean(dists)

    # break upon convergence
    abs(Î´new - Î´cur) / Î´cur < tol && break

    # update and continue
    Î´cur = Î´new
    iter += 1
  end

  orphans = findall(iszero, labels)
  if length(orphans) > 0
    assigned = findall(!iszero, labels)
    ğ’Ÿâ‚€ = view(ğ’Ÿ, assigned)
    csearcher = KNearestSearch(ğ’Ÿâ‚€, 1)

    for orphan in orphans
      p = centroid(ğ’Ÿ, orphan)
      i = search(p, csearcher)[1]
      labels[orphan] = labels[assigned[i]]
    end
  end

  newtable = (; label=labels)
  newgeotable = georef(newtable, domain(geotable))

  newgeotable, nothing
end

slic_spacing(ğ’Ÿ, k) = slic_srecursion(k, sides(boundingbox(ğ’Ÿ)))

# given the desired number of clusters and the sides of the bounding box
# of the domain, returns the spacing for each dimension recursively
function slic_srecursion(k, l)
  d = length(l)

  # base case
  d == 1 && return [l[1] / k]

  # compute the spacing for the j-th dimension
  j = argmax(l)
  kâ±¼ = ceil(Int, k^(1 / d))
  sâ±¼ = l[j] / kâ±¼

  # update the new k and l
  kâ‚™ = ceil(Int, k / kâ±¼)
  lâ‚™ = l[[1:(j - 1); (j + 1):d]]

  # then recursively compute the spacing for the remaining dimensions
  s = slic_srecursion(kâ‚™, lâ‚™)

  [s[begin:(j - 1)]; [sâ±¼]; s[j:end]]
end

function slic_initialization(ğ’Ÿ, s)
  # efficient neighbor search
  searcher = KNearestSearch(ğ’Ÿ, 1)

  # bounding box properties
  bbox = boundingbox(ğ’Ÿ)
  lo, up = to.(extrema(bbox))

  # cluster centers
  clusters = Vector{Int}()
  neighbor = Vector{Int}(undef, 1)
  ranges = [(l + sáµ¢ / 2):sáµ¢:u for (l, sáµ¢, u) in zip(lo, s, up)]
  for x in Iterators.product(ranges...)
    search!(neighbor, Point(x), searcher)
    push!(clusters, neighbor[1])
  end

  unique(clusters)
end

function slic_assignment!(geotable, searcher, td, m, s, centers, labels, dists)
  sâ‚˜ = maximum(s)
  ğ’Ÿ = domain(geotable)
  for (k, câ‚–) in enumerate(centers)
    inds = search(centroid(ğ’Ÿ, câ‚–), searcher)

    # distance between coordinates
    X = (to(centroid(ğ’Ÿ, i)) for i in inds)
    xâ‚– = [to(centroid(ğ’Ÿ, câ‚–))]
    dâ‚› = pairwise(Euclidean(), X, xâ‚–)

    # distance between variables
    ğ’®áµ¢ = view(geotable, inds)
    ğ’®â‚– = view(geotable, [câ‚–])
    V = values(ğ’®áµ¢)
    vâ‚– = values(ğ’®â‚–)
    dáµ¥ = pairwise(td, V, vâ‚–)

    # total distance
    dâ‚œ = @. âˆš(dáµ¥^2 + m^2 * (dâ‚› / sâ‚˜)^2)

    @inbounds for (i, ind) in enumerate(inds)
      if dâ‚œ[i] < dists[ind]
        dists[ind] = dâ‚œ[i]
        labels[ind] = k
      end
    end
  end
end

function slic_update!(geotable, centers, labels)
  ğ’Ÿ = domain(geotable)
  for k in eachindex(centers)
    inds = findall(isequal(k), labels)
    X = (to(centroid(ğ’Ÿ, i)) for i in inds)
    xâ‚– = [mean(X)]
    dâ‚› = pairwise(Euclidean(), X, xâ‚–)
    @inbounds centers[k] = inds[argmin(vec(dâ‚›))]
  end
end
