# ------------------------------------------------------------------
# Licensed under the MIT License. See LICENSE in the project root.
# ------------------------------------------------------------------

"""
    SLIC(k, m; tol=1e-4, maxiter=10, weights=nothing, as=:cluster)

A transform for clustering geospatial data into approximately `k`
clusters using Simple Linear Iterative Clustering (SLIC).

The transform produces clusters of samples that are spatially
connected based on a distance `dâ‚›` and that, at the same time,
are similar in terms of `vars` with distance `dáµ¥`. The tradeoff
is controlled with a hyperparameter `m` in an additive model
`dâ‚œ = âˆš(dáµ¥Â² + mÂ²(dâ‚›/s)Â²)`.

## Parameters

* `k`       - Approximate number of clusters
* `m`       - Hyperparameter of SLIC model

## Options

* `tol`     - Tolerance of k-means algorithm (default to `1e-4`)
* `maxiter` - Maximum number of iterations (default to `10`)
* `weights` - Dictionary with weights for each attribute (default to `nothing`)
* `as`      - Variable name used to store clustering results

## References

* Achanta et al. 2011. [SLIC superpixels compared to state-of-the-art
  superpixel methods](https://ieeexplore.ieee.org/document/6205760)
"""
struct SLIC{W} <: TableTransform
  k::Int
  m::Float64
  tol::Float64
  maxiter::Int
  weights::W
  as::Symbol
end

function SLIC(k::Int, m::Real; tol=1e-4, maxiter=10, weights=nothing, as=:cluster)
  @assert tol > 0 "tolerance must be positive"
  @assert maxiter > 0 "maximum number of iterations must be positive"
  SLIC{typeof(weights)}(k, m, tol, maxiter, weights, Symbol(as))
end

function apply(transform::SLIC, geotable::AbstractGeoTable)
  # retrieve parameters
  w = transform.weights
  m = transform.m

  # normalize attributes
  ğ’¯ = TableDistances.normalize(values(geotable))
  Î© = georef(first(ğ’¯), domain(geotable))
  ğ’Ÿ = domain(Î©)

  # initial spacing of clusters
  s = slic_spacing(ğ’Ÿ, transform)

  # initialize cluster centers
  c = slic_initialization(ğ’Ÿ, s)

  # ball neighborhood search
  searcher = BallSearch(ğ’Ÿ, MetricBall(maximum(s)))

  # pre-allocate memory for label and distance
  l = fill(0, nelements(ğ’Ÿ))
  d = fill(Inf, nelements(ğ’Ÿ))

  # performance parameters
  tol = transform.tol
  maxiter = transform.maxiter

  # Lloyd's (a.k.a. k-means) algorithm
  err, iter = Inf, 0
  while err > tol && iter < maxiter
    o = copy(c)

    slic_assignment!(Î©, searcher, w, m, s, c, l, d)
    slic_update!(Î©, c, l)

    err = norm(c - o) / norm(o)
    iter += 1
  end

  orphans = findall(iszero, l)
  if length(orphans) > 0
    assigned = findall(!iszero, l)
    ğ’Ÿâ‚€ = view(ğ’Ÿ, assigned)
    csearcher = KNearestSearch(ğ’Ÿâ‚€, 1)

    for orphan in orphans
      p = centroid(ğ’Ÿ, orphan)
      i = search(p, csearcher)[1]
      l[orphan] = l[assigned[i]]
    end
  end

  newtable = (; transform.as => categorical(l))
  newgeotable = georef(newtable, domain(geotable))

  newgeotable, nothing
end

slic_spacing(ğ’Ÿ, transform) = slic_srecursion(transform.k, sides(boundingbox(ğ’Ÿ)))

# given the desired number of clusters and the sides of the bounding box
# of the domain, returns the spacing for each dimension recursively
function slic_srecursion(k, l)
  d = length(l)

  # base case
  d == 1 && return [l[1] / k]

  # compute the spacing for the j-th dimension
  j = argmax(l)
  kâ±¼ = ceil(Int, k^(1 / d))
  sâ±¼ = l[j] / kâ±¼

  # update the new k and l
  kâ‚™ = ceil(Int, k / kâ±¼)
  lâ‚™ = l[[1:(j - 1); (j + 1):d]]

  # then recursively compute the spacing for the remaining dimensions
  s = slic_srecursion(kâ‚™, lâ‚™)

  [s[begin:(j - 1)]; [sâ±¼]; s[j:end]]
end

function slic_initialization(ğ’Ÿ, s)
  # efficient neighbor search
  searcher = KNearestSearch(ğ’Ÿ, 1)

  # bounding box properties
  bbox = boundingbox(ğ’Ÿ)
  lo, up = to.(extrema(bbox))

  # cluster centers
  clusters = Vector{Int}()
  neighbor = Vector{Int}(undef, 1)
  ranges = [(l + sáµ¢ / 2):sáµ¢:u for (l, sáµ¢, u) in zip(lo, s, up)]
  for x in Iterators.product(ranges...)
    search!(neighbor, Point(x), searcher)
    push!(clusters, neighbor[1])
  end

  unique(clusters)
end

function slic_assignment!(geotable, searcher, w, m, s, c, l, d)
  sâ‚˜ = maximum(s)
  ğ’Ÿ = domain(geotable)
  for (k, câ‚–) in enumerate(c)
    inds = search(centroid(ğ’Ÿ, câ‚–), searcher)

    # distance between coordinates
    X = (to(centroid(ğ’Ÿ, i)) for i in inds)
    xâ‚– = [to(centroid(ğ’Ÿ, câ‚–))]
    dâ‚› = pairwise(Euclidean(), X, xâ‚–)

    # distance between variables
    ğ’®áµ¢ = view(geotable, inds)
    ğ’®â‚– = view(geotable, [câ‚–])
    V = values(ğ’®áµ¢)
    vâ‚– = values(ğ’®â‚–)
    dáµ¥ = pairwise(TableDistance(normalize=false, weights=w), V, vâ‚–)

    # total distance
    dâ‚œ = @. âˆš(dáµ¥^2 + m^2 * (dâ‚› / sâ‚˜)^2)

    @inbounds for (i, ind) in enumerate(inds)
      if dâ‚œ[i] < d[ind]
        d[ind] = dâ‚œ[i]
        l[ind] = k
      end
    end
  end
end

function slic_update!(geotable, c, l)
  ğ’Ÿ = domain(geotable)
  for k in eachindex(c)
    inds = findall(isequal(k), l)
    X = (to(centroid(ğ’Ÿ, i)) for i in inds)
    xâ‚– = [mean(X)]
    dâ‚› = pairwise(Euclidean(), X, xâ‚–)
    @inbounds c[k] = inds[argmin(vec(dâ‚›))]
  end
end
